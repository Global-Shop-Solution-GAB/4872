Program.Sub.ScreenSU.Start
Gui.frmOptions..Create
Gui.frmOptions..Caption("Ship Date Calculation - Options")
Gui.frmOptions..Size(6600,7035)
Gui.frmOptions..MinX(0)
Gui.frmOptions..MinY(0)
Gui.frmOptions..Position(0,0)
Gui.frmOptions..BackColor(-2147483633)
Gui.frmOptions..MousePointer(0)
Gui.frmOptions..Event(UnLoad,frmOptions_UnLoad)
Gui.frmOptions.GsGCOptions.Create(GsGridControl)
Gui.frmOptions.GsGCOptions.Size(6450,6315)
Gui.frmOptions.GsGCOptions.Position(15,15)
Gui.frmOptions.GsGCOptions.Anchor(15)
Gui.frmOptions.GsGCOptions.Event(RowCellClick,GsGCOptions_RowCellClick)
Gui.frmOptions.cmdSave.Create(Button)
Gui.frmOptions.cmdSave.Size(855,375)
Gui.frmOptions.cmdSave.Position(5610,6360)
Gui.frmOptions.cmdSave.Anchor(10)
Gui.frmOptions.cmdSave.Caption("Save")
Gui.frmOptions.cmdSave.Event(Click,cmdSave_Click)
Gui.frmOptions.lblStatus.Create(Label,"Save Complete",False,1215,255,0,4380,6480,True,0,"Arial",8,-2147483633,0)
Gui.frmOptions.cmdCleanup.Create(Button)
Gui.frmOptions.cmdCleanup.Size(855,375)
Gui.frmOptions.cmdCleanup.Position(15,6360)
Gui.frmOptions.cmdCleanup.Anchor(10)
Gui.frmOptions.cmdCleanup.Caption("Cleanup")
Gui.frmOptions.cmdCleanup.ToolTip("Remove Lock and Temp WO")
Gui.frmOptions.cmdCleanup.Event(Click,cmdCleanup_Click)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
		
	'Open Connection
	F.ODBC.Connection!con.OpenCompanyConnection
		
	'Show Screen
	Gui.frmOptions..Show
	
	'Build Grid
	F.Intrinsic.Control.CallSub(BuildGrid)
	
	'Load Options
	F.Intrinsic.Control.CallSub(LoadOptions)
	
	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.frmOptions_UnLoad.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare

	F.ODBC.Connection!con.Close
	F.Intrinsic.Control.End

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.frmOptions_UnLoad.End

Program.Sub.BuildGrid.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.iCnt.Declare
	V.Local.sColumns.Declare

	F.Data.DataTable.Create("dtOpts",True)
	F.Data.DataTable.AddColumn("dtOpts","ID",Long)
	F.Data.DataTable.AddColumn("dtOpts","Option",String)
	F.Data.DataTable.AddColumn("dtOpts","Value",Boolean,False)
	F.Data.DataTable.AddRow("dtOpts","ID",5000,"Option","Validate Terms (Double click to set Terms)")
	F.Data.DataTable.AddRow("dtOpts","ID",5100,"Option","Validate Order Qty (Prevents Save)")
	F.Data.DataTable.AddRow("dtOpts","ID",5200,"Option","Validate Price > Cost")
	F.Data.DataTable.AddRow("dtOpts","ID",5300,"Option","Validate a WO is gernerated (Prevents Save)")
	F.Data.DataTable.AddRow("dtOpts","ID",5400,"Option","Validate NET before Calculating Ship Date")
	F.Data.DataTable.AddRow("dtOpts","ID",5500,"Option","Apply to Quotes")
	F.Data.DataTable.AddRow("dtOpts","ID",5600,"Option","Prevent Save if Calc Ship Date has not been ran")
	
	'Set and Format Grid
	Gui.frmOptions.GsGCOptions.AddGridviewFromDatatable("gvOpts","dtOpts")
	Gui.frmOptions.GsGCOptions.SuspendLayout()
	Gui.frmOptions.GsGCOptions.SetGridViewProperty("gvOpts","MultiSelect",True)
	Gui.frmOptions.GsGCOptions.SetGridViewProperty("gvOpts","AllowSort",True)
	Gui.frmOptions.GsGCOptions.SetGridViewProperty("gvOpts","AllowFilter",True)
	Gui.frmOptions.GsGCOptions.SetGridViewProperty("gvOpts","OptionsDetailShowDetailTabs",False)
	Gui.frmOptions.GsGCOptions.SetGridViewProperty("gvOpts","OptionsViewShowGroupPanel",False)
	Gui.frmOptions.GsGCOptions.SetGridViewProperty("gvOpts","EnableAppearanceOddRow",True)
	Gui.frmOptions.GsGCOptions.SetGridViewProperty("gvOpts","OptionsFindAlwaysVisible",False)
	
	'Loop through columns names by using the V.DataTable.dtName.FieldNames
	F.Intrinsic.String.Split(V.DataTable.dtOpts.FieldNames,"*!*",V.Local.sColumns)
	F.Intrinsic.Control.For(V.Local.iCnt,0,V.Local.sColumns.UBound,1)
		Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts",V.Local.sColumns(V.Local.iCnt),"HeaderFontBold",True)
	F.Intrinsic.Control.Next(V.Local.iCnt)
	
	'ID
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","ID","Visible",False)
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","ID","Caption","ID")
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","ID","MinWidth","55")
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","ID","AllowEdit",False)
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","ID","ReadOnly",True)

	'Option
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","Option","Caption","Option")
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","Option","MinWidth","225")
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","Option","AllowEdit",False)
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","Option","ReadOnly",True)
	
	'Value
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","Value","ShowCaption",False)
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","Value","Caption","Value")
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","Value","MinWidth","25")
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","Value","AllowEdit",True)
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","Value","ReadOnly",False)
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","Value","HeaderHAlignment","Left")
	Gui.frmOptions.GsGCOptions.SetColumnProperty("gvOpts","Value","CellHAlignment","Left")
	
	Gui.frmOptions.GsGCOptions.ResumeLayout()
	Gui.frmOptions.GsGCOptions.MainView("gvOpts")

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.BuildGrid.End

Program.Sub.GsGCOptions_RowCellClick.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.sTerms.Declare
	V.Local.sCaption.Declare

	F.Intrinsic.Control.If(V.Args.Clicks,=,2)
		F.Intrinsic.Control.If(V.Args.Column,=,"Option")
		F.Intrinsic.Control.AndIf(V.DataTable.dtOpts(V.Args.RowIndex).ID!FieldVal,=,5000)
			F.Intrinsic.UI.Browser("Select Terms:","con","Select Terms_Code,Terms_Message From V_AR_Terms;","Terms*!*Description","100*!*300",V.Local.sTerms)
			F.Intrinsic.Control.If(V.Local.sTerms.IsNotCancel)
				F.Intrinsic.String.Split(V.Local.sTerms,"*!*",V.Local.sTerms)
				F.Intrinsic.String.Build("Validate Terms - ({0}) - (Double click to change Terms)",V.Local.sTerms(1).Trim,V.Local.sCaption)
				F.Data.DataTable.SetValue("dtOpts",V.Args.RowIndex,"Option",V.Local.sCaption)
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.EndIf

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.GsGCOptions_RowCellClick.End

Program.Sub.LoadOptions.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.iCnt.Declare
	V.Local.sRet.Declare
	V.Local.bRet.Declare
	
	F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5000,5,"",V.Local.sRet)
	F.Intrinsic.Control.If(V.Local.sRet,=,"")
		F.Data.DataTable.SetValue("dtOpts",-1,"Value",True)
		F.Intrinsic.Control.CallSub(cmdSave_Click)
	F.Intrinsic.Control.EndIf
	
	F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5600,5,"",V.Local.sRet)
	F.Intrinsic.Control.If(V.Local.sRet,=,"")
		F.Global.Registry.AddValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5600,False,V.DataTable.dtOpts(6).Option!FieldVal,True,0,-999.0,1/1/1980,12:00:00 AM)
	F.Intrinsic.Control.EndIf

	F.Intrinsic.Control.For(V.Local.iCnt,0,V.DataTable.dtOpts.RowCount--,1)
		F.Intrinsic.Control.SelectCase(V.DataTable.dtOpts(V.Local.iCnt).ID!FieldVal)
			F.Intrinsic.Control.Case(5000)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5000,0,"",V.Local.bRet)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5000,5,"",V.Local.sRet)
				
			F.Intrinsic.Control.Case(5100)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5100,0,"",V.Local.bRet)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5100,5,"",V.Local.sRet)
				
			F.Intrinsic.Control.Case(5200)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5200,0,"",V.Local.bRet)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5200,5,"",V.Local.sRet)
				
			F.Intrinsic.Control.Case(5300)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5300,0,"",V.Local.bRet)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5300,5,"",V.Local.sRet)
				
			F.Intrinsic.Control.Case(5400)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5400,0,"",V.Local.bRet)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5400,5,"",V.Local.sRet)
				
			F.Intrinsic.Control.Case(5500)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5500,0,"",V.Local.bRet)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5500,5,"",V.Local.sRet)
				
			F.Intrinsic.Control.Case(5600)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5600,0,"",V.Local.bRet)
				F.Global.Registry.ReadValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5600,5,"",V.Local.sRet)
			
		F.Intrinsic.Control.EndSelect
		F.Data.DataTable.SetValue("dtOpts",V.Local.iCnt,"Option",V.Local.sRet,"Value",V.Local.bRet)
	F.Intrinsic.Control.Next(V.Local.iCnt)
	
	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.LoadOptions.End

Program.Sub.cmdSave_Click.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.iCnt.Declare

	F.Intrinsic.Control.For(V.Local.iCnt,0,V.DataTable.dtOpts.RowCount--,1)
		F.Intrinsic.Control.SelectCase(V.DataTable.dtOpts(V.Local.iCnt).ID!FieldVal)
			F.Intrinsic.Control.Case(5000)
				F.Global.Registry.AddValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5000,False,V.DataTable.dtOpts(V.Local.iCnt).Option!FieldVal,V.DataTable.dtOpts(V.Local.iCnt).Value!FieldVal,0,-999.0,1/1/1980,12:00:00 AM)
				
			F.Intrinsic.Control.Case(5100)
				F.Global.Registry.AddValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5100,False,V.DataTable.dtOpts(V.Local.iCnt).Option!FieldVal,V.DataTable.dtOpts(V.Local.iCnt).Value!FieldVal,0,-999.0,1/1/1980,12:00:00 AM)
				
			F.Intrinsic.Control.Case(5200)
				F.Global.Registry.AddValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5200,False,V.DataTable.dtOpts(V.Local.iCnt).Option!FieldVal,V.DataTable.dtOpts(V.Local.iCnt).Value!FieldVal,0,-999.0,1/1/1980,12:00:00 AM)
				
			F.Intrinsic.Control.Case(5300)
				F.Global.Registry.AddValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5300,False,V.DataTable.dtOpts(V.Local.iCnt).Option!FieldVal,V.DataTable.dtOpts(V.Local.iCnt).Value!FieldVal,0,-999.0,1/1/1980,12:00:00 AM)
				
			F.Intrinsic.Control.Case(5400)
				F.Global.Registry.AddValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5400,False,V.DataTable.dtOpts(V.Local.iCnt).Option!FieldVal,V.DataTable.dtOpts(V.Local.iCnt).Value!FieldVal,0,-999.0,1/1/1980,12:00:00 AM)
				
			F.Intrinsic.Control.Case(5500)
				F.Global.Registry.AddValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5500,False,V.DataTable.dtOpts(V.Local.iCnt).Option!FieldVal,V.DataTable.dtOpts(V.Local.iCnt).Value!FieldVal,0,-999.0,1/1/1980,12:00:00 AM)
				F.Intrinsic.Control.CallSub(ActivateQuotes,"Activate",V.DataTable.dtOpts(V.Local.iCnt).Value!FieldVal)
			
			F.Intrinsic.Control.Case(5600)
				F.Global.Registry.AddValue("SUPERVSR",V.Caller.CompanyCode,"4872",4872,5600,False,V.DataTable.dtOpts(V.Local.iCnt).Option!FieldVal,V.DataTable.dtOpts(V.Local.iCnt).Value!FieldVal,0,-999.0,1/1/1980,12:00:00 AM)
				
		F.Intrinsic.Control.EndSelect
	F.Intrinsic.Control.Next(V.Local.iCnt)
	
	Gui.frmOptions.lblStatus.Visible(True)
	F.Intrinsic.UI.Sleep(1)
	Gui.frmOptions.lblStatus.Visible(False)

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.cmdSave_Click.End

Program.Sub.ActivateQuotes.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.sHooks.Declare
	V.Local.sHook.Declare
	V.Local.sFile.Declare
	V.Local.iCnt.Declare
	V.Local.iRet.Declare
	V.Local.sSQL.Declare
	V.Local.sSeq.Declare
	
	F.Intrinsic.String.Split("10260!*!GAB_4872_Ship_Date_Calc.g2u*!*10670!*!GAB_4872_Ship_Date_Calc.g2u*!*10920!*!GAB_4872_Warnings.g2u*!*10531!*!GAB_4872_Warnings.g2u","*!*",V.Local.sHooks)
	F.Intrinsic.Control.For(V.Local.iCnt,0,V.Local.sHooks.UBound,1)
		F.Intrinsic.String.Split(V.Local.sHooks(V.Local.iCnt),"!*!",V.Local.sHook)
		V.Local.sFile.Set(V.Local.sHook(1))
		V.Local.sHook.Set(V.Local.sHook(0))
		F.Intrinsic.Control.If(V.Args.Activate)
			'Activate Hook
			F.Intrinsic.String.Build("Select Seq From V_Hook_Association Where ID = '0000{0}' And Script_Name = '{1}';",V.Local.sHook,V.Local.sFile,V.Local.sSQL)
			F.ODBC.Connection!con.ExecuteAndReturn(V.Local.sSQL,V.Local.sSeq)
			F.Intrinsic.Control.If(V.Ambient.ExecuteAndReturnEOF,=,True)
				F.Global.Hook.AddHookSequenceAssociation(V.Local.sHook,True,"#SCRIPT-PATH#",V.Local.sFile,False,"G",2.00,"","",V.Local.iRet)
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.Else
			'Delete Hook
			F.Intrinsic.String.Build("Select Seq From V_Hook_Association Where ID = '0000{0}' And Script_Name = '{1}';",V.Local.sHook,V.Local.sFile,V.Local.sSQL)
			F.ODBC.Connection!con.ExecuteAndReturn(V.Local.sSQL,V.Local.sSeq)
			F.Intrinsic.Control.If(V.Ambient.ExecuteAndReturnEOF,=,False)
				F.Intrinsic.Control.If(V.Local.sSeq,<>,"")
					F.Intrinsic.String.Build("Delete From Hook_Association Where ID = '0000{0}' And Script_Name = '{1}' And Seq = '{2}';",V.Local.sHook,V.Local.sFile,V.Local.sSeq,V.Local.sSQL)
					F.ODBC.Connection!con.Execute(V.Local.sSQL)
				F.Intrinsic.Control.EndIf
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Next(V.Local.iCnt)

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.ActivateQuotes.End

Program.Sub.cmdCleanup_Click.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.bFile.Declare
	V.Local.sFile.Declare
	V.Local.sDeleteFile.Declare
	V.Local.iCnt.Declare
	
	'Get files from FILES and delete
	F.Intrinsic.String.Build("{0}\GAB_4872_*",V.Caller.FilesDir,V.Local.sFile)
	F.Intrinsic.File.GetFileList(V.Local.sFile,V.Local.sFile)
	F.Intrinsic.Control.If(V.Local.sFile,<>,"")
		F.Intrinsic.String.Split(V.Local.sFile,":",V.Local.sFile)
		F.Intrinsic.Control.For(V.Local.iCnt,0,V.Local.sFile.UBound,1)
			F.Intrinsic.String.Build("{0}\{1}",V.Caller.FilesDir,V.Local.sFile(V.Local.iCnt),V.Local.sDeleteFile)
			F.Intrinsic.File.DeleteFile(V.Local.sDeleteFile)
		F.Intrinsic.Control.Next(V.Local.iCnt)
	F.Intrinsic.Control.EndIf
	
	'Get files from Temp\GSS and delete
	F.Intrinsic.String.Build("{0}GAB_4872_*",V.Caller.LocalGSSTempDir,V.Local.sFile)
	F.Intrinsic.File.GetFileList(V.Local.sFile,V.Local.sFile)
	F.Intrinsic.Control.If(V.Local.sFile,<>,"")
		F.Intrinsic.String.Split(V.Local.sFile,":",V.Local.sFile)
		F.Intrinsic.Control.For(V.Local.iCnt,0,V.Local.sFile.UBound,1)
			F.Intrinsic.String.Build("{0}{1}",V.Caller.LocalGSSTempDir,V.Local.sFile(V.Local.iCnt),V.Local.sDeleteFile)
			F.Intrinsic.File.DeleteFile(V.Local.sDeleteFile)
		F.Intrinsic.Control.Next(V.Local.iCnt)
	F.Intrinsic.Control.EndIf

	'Remove Temp WO
	F.ODBC.Connection!con.Execute("Delete From Job_Header Where Job = '###001';")
	F.ODBC.Connection!con.Execute("Delete From Job_Operations Where Job = '###001';")
	F.ODBC.Connection!con.Execute("Delete From Job_Commitments Where Job = '###001';")
	F.ODBC.Connection!con.Execute("Delete From APSV3_JBMASTER Where Job = '###001';")
	F.ODBC.Connection!con.Execute("Delete From APSV3_JBLINES Where Job = '###001';")
	F.ODBC.Connection!con.Execute("Delete From APSV3_UBDETAIL Where Job = '###001';")
	
	'Complete
	F.Intrinsic.UI.Msgbox("Cleanup Complete.","Complete")

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.cmdCleanup_Click.End

Program.Sub.Comments.Start
Program.Sub.Comments.End